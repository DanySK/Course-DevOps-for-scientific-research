buildscript {
  repositories { maven { url "https://plugins.gradle.org/m2/" } }
  dependencies { classpath "com.gradle:build-scan-plugin:$buildScanVersion" }
}
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'build-dashboard'
apply plugin: 'com.gradle.build-scan'
apply plugin: 'maven'

task fatJar(type: Jar, dependsOn: subprojects.compileJava) {
    manifest {
        attributes 'Implementation-Title': 'Redistributable JAR',
        'Implementation-Version': rootProject.version,
        'Main-Class': 'MyMain'
    }
    baseName = "${rootProject.name}-executable"
    from(configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }) {
        // remove all signature files
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude '.gradle'
        exclude 'build.gradle'
        exclude 'gradle'
        exclude 'gradlew'
        exclude 'gradlew.bat'
    }
    with jar
}
build.dependsOn(fatJar)
sourceCompatibility = "$jdkVersion"
build.dependsOn(jacocoTestReport)
build.dependsOn(buildDashboard)
buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}
repositories {
    mavenCentral()
}
dependencies {
    testImplementation "junit:junit:$junitVersion"
}
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://$buildDir/maven-format/")
            pom.project {
                name artifactId
                description projectDescription
                def ref = "${scmRootUrl}/${artifactId}"
                packaging 'jar'
                url ref
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                scm {
                    url ref
                    def scmRef = "${scmType}:${scmLogin}/${scmRepoName}"
                    connection scmRef
                    developerConnection scmRef
                }
                developers {
                    developer {
                        name 'Danilo Pianini'
                        email 'danilo.pianini@unibo.it'
                        url 'http://www.danilopianini.org'
                    }
                }
                contributors {
                    contributor {
                        name 'Jacob Beal'
                        email 'jakebeal@bbn.com'
                        url 'http://web.mit.edu/jakebeal/www/'
                    }
                }
            }
        }
    }
}
